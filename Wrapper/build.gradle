apply plugin: 'maven-publish'
apply plugin: 'org.moddingx.ljc'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

jar {
    manifest.attributes([
            "Automatic-Module-Name": "org.moddingx.cursewrapper",
            "Implementation-Version": project.version
    ])
}

// Check, that LJC can convert to java 11 (required for Moonstone)
task legacyJar(type: org.moddingx.ljc.gradle.LjcTask) {
    input = jar.archiveFile
    languageLevel = 11
}
jar.finalizedBy legacyJar

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    from project(':CurseWrapperShared').sourceSets.main.allSource
}
build.dependsOn sourcesJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url rootProject.file('../moddingx/maven')
        }
    }
}
