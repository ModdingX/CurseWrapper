plugins {
    id 'application'
    id 'maven-publish'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.zoomba-lang:spark-core:3.0'
    implementation 'com.google.guava:guava:33.2.1-jre'

    implementation 'org.slf4j:slf4j-api:2.0.13'
    runtimeOnly 'org.slf4j:slf4j-jdk14:2.0.13'
    
    implementation(platform('org.eclipse.jetty:jetty-bom')) { version { strictly '[11.0.21,12.0.0)' } }
}

application.mainClass = "org.moddingx.cursewrapper.Main"

jar {
    manifest.attributes([
            "Main-Class": application.mainClass.get(),
            "Automatic-Module-Name": "org.moddingx.cursewrapper.server",
            "Implementation-Version": project.version
    ])
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    from project(':CurseWrapperShared').sourceSets.main.allSource
}
build.dependsOn sourcesJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name 'moddingx'
            url 'https://maven.moddingx.org/release'
            credentials(PasswordCredentials)
        }
    }
}
